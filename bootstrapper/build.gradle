import org.ajoberstar.gradle.git.tasks.*
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
	repositories {
		mavenCentral()
	}
	
	dependencies {
		classpath 'org.ajoberstar:gradle-git:0.6.3'
	}
}

dependencies {
	testCompile configurations.testPack
}


def versionInfo = [
	friendlyname: 'JibbrJabbr',
	version: project.version,
	'build-time': System.currentTimeMillis() + ''
]

task readBranch(type: GitBranchList) << {
	versionInfo.branch = workingBranch.name
}

task readLog(type: GitLog) {
	maxCommits 1
	doLast {
		log.each {
			versionInfo['commit-id'] = it.id
			versionInfo['commit-message'] = it.shortMessage
			versionInfo['commit-user'] = it.committer.name
			versionInfo['commit-email'] = it.committer.email
			versionInfo['commit-time'] = it.time + '000'
		}
	}
}

processResources {
	outputs.upToDateWhen {
		false // we always want the build time, so it's always out of date
	}
	filter ReplaceTokens, tokens: versionInfo
}

processResources.dependsOn readLog, readBranch