import org.ajoberstar.grgit.*
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
	repositories {
		mavenCentral()
	}
	
	dependencies {
		classpath 'org.ajoberstar:gradle-git:0.9.0'
	}
}

dependencies {
	testCompile configurations.testPack
}

ext.repo = Grgit.open(rootProject.file('.'))

def commit = repo.log(maxCommits: 1)[0]

def versionInfo = [
	friendlyname: 'JibbrJabbr',
	version: project.version,
	branch: repo.branch.current.name,
	commitUserName: commit.committer.name,
	commitUserEmail: commit.committer.email,
	commitId: commit.id,
	commitMessage: commit.shortMessage,
	commitTime: commit.time + "000"
]

processResources {
	outputs.upToDateWhen {
		
		def version = file(outputs.files.singleFile.path + File.separator + 'jj' + File.separator + 'VERSION')
		Set<String> values = new HashSet<String>(versionInfo.values());
		if (version.exists()) {
			version.eachLine('UTF-8') {
				values.remove(it);
			}
		}
		version.exists() && values.empty
	}
	filter ReplaceTokens, tokens: versionInfo
}
