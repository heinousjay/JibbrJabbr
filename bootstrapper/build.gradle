import org.ajoberstar.gradle.git.tasks.*
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
	repositories {
		mavenCentral()
	}
	
	dependencies {
		classpath 'org.ajoberstar:gradle-git:0.6.3'
	}
}

dependencies {
	testCompile configurations.testPack
}


def versionInfo = [
	friendlyname: 'JibbrJabbr',
	version: project.version
]

task readBranch(type: GitBranchList) << {
	versionInfo.branch = workingBranch.name ?: 'master'
}

task readLog(type: GitLog) {
	maxCommits 1
	doLast {
		log.each {
			versionInfo['commit-id'] = it.id
			versionInfo['commit-message'] = it.shortMessage
			versionInfo['commit-user'] = it.committer.name
			versionInfo['commit-email'] = it.committer.email
			versionInfo['commit-time'] = it.time + '000'
		}
	}
}

processResources {
	outputs.upToDateWhen {
		
		def version = file(outputs.files.singleFile.path + File.separator + 'jj' + File.separator + 'VERSION')
		Set<String> values = new HashSet<String>(versionInfo.values());
		if (version.exists()) {
			version.eachLine('UTF-8') {
				values.remove(it);
			}
		}
		version.exists() && values.empty
	}
	filter ReplaceTokens, tokens: versionInfo
}

processResources.dependsOn readLog, readBranch